ext {
    def hasVersionNumber = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = hasVersionNumber ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    teamcityVersion = findProperty('teamcityVersion') ?: '2024.12'
    projectIds = ['group': 'com.macstadium.orka', 'version': versionNumber]
}

group = projectIds.group
version = projectIds.version

repositories {
    mavenCentral()
}

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    checkstyle {
        toolVersion = '10.12.5'  // Latest stable version compatible with Java 21
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = true  // Ignore legacy style violations
        maxWarnings = 1000
        showViolations = true
    }

    tasks.named('test') {
        useTestNG()
    }
    tasks.named('jar') {
        archiveVersion.set('')
    }
}


task createGitTag(type:Exec) {
    commandLine './create-git-tag.sh'
    args findProperty('versionNumber')
}
