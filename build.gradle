ext {
    def hasVersionNumber = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = hasVersionNumber ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    teamcityVersion = findProperty('teamcityVersion') ?: '2018.1'
    projectIds = ['group': 'com.macstadium.orka', 'version': versionNumber]
}

group = projectIds.group
version = projectIds.version

repositories {
    mavenCentral()
}

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    checkstyle {
        toolVersion = '8.45.1'  // Стабильная версия, совместимая с Java 8-17
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = true  // Игнорируем старые нарушения стиля
        maxWarnings = 1000
        showViolations = true
    }

    test.useTestNG()
    jar.version = null
}


task createGitTag(type:Exec) {
    commandLine './create-git-tag.sh'
    args findProperty('versionNumber')
}
